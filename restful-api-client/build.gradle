group 'io.testproject'
version '1.5.6'

apply plugin: 'java'

// If necessary, update the location of TestProject SDK JAR file
def TP_SDK = System.getenv('TP_SDK')

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

// Configurations
configurations {
    tpsdk
    compile.extendsFrom tpsdk
}

// JAR Task
jar {
    assert file("${TP_SDK}").exists(): "TestProject SDK JAR file was not found, please update the TP_SDK variable"
    archiveName "${rootProject.name}-${version}.jar"
    dependsOn configurations.runtime
    from {
        // Removes TestProject SDK from the final jar file
        (configurations.runtime - configurations.tpsdk).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    // Extract SDK version
    from {
        (configurations.tpsdk).collect {
            zipTree(it).matching {
                include 'testproject-sdk.properties'
            }
        }
    }

}

test {
    useJUnitPlatform()
}

dependencies {
    tpsdk files("${TP_SDK}")
    compile 'com.github.everit-org.json-schema:org.everit.json.schema:1.12.1'


    compile group: 'org.json', name: 'json', version: '20180813'
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'
    compile 'org.glassfish.jersey.media:jersey-media-multipart:2.22.1'



    // JUnit 5
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntime 'org.junit.platform:junit-platform-runner:1.3.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testRuntime 'org.junit.vintage:junit-vintage-engine:5.3.1'



}