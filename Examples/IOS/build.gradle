group 'io.testproject'
version '1.1'

apply plugin: 'java'

// If necessary, update the location of TestProject SDK JAR file
def TP_SDK = '../../io.testproject.sdk.java.jar'

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

configurations {
    testImplementation.extendsFrom compileOnly
}

dependencies {
    testCompileOnly 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.3.1'
    compileOnly files("${TP_SDK}")
}

// JAR Task
jar {
    assert file("${TP_SDK}").exists(): "TestProject SDK JAR file was not found, please update the TP_SDK variable"
    archiveName "${rootProject.name}-${version}.jar"

    // Build "uber" JAR, excluding compileOnly dependencies
    from(
            configurations.runtimeClasspath.collect {
                it.isDirectory ? it : zipTree(it)
            }
    )
    // Include the TP SDK version in the JAR
    from(
            configurations.compileClasspath.collect {
                zipTree(it).matching {
                    include 'testproject-sdk.properties'
                }
            }
    )
}